/*************************************************************************************************\
* Наименование:  отчеты на DSQL                                                                   *
* Контур/Модуль: Утилиты                                                                          *
* Примечание:                                                                                     *
* Вид работы  |Номер         |Дата    |Исполнитель              |Проект                           *
* ----------------------------------------------------------------------------------------------- *
\*************************************************************************************************/

#include xlReport.Vih
#include ViewResult.VIH
#include SQLBuildReport.vih
#include SQLPrintToXLT.vih
#include SQLFilts.vih
#include GetKau.vih            // Обьектный интерфес выбора КАУ

Interface SQLPrintToXLT 'Печать отчета в Exel-шаблон', EscClose;

#include GetKau.var     // Переменная типа "Интерфеса выбора КАУ"
create view
var
  pxlrepsetup              : xlrepsetup;
  pXL                      : XLSRepBuilder;      // Excel
  sXLTFileName,ID          : string;
  sXLSFileName             : string;
  iSqlFilts                : SqlFilts;

  wSQLBuildReport : SQLBuildReport;
as select *
from
OborCfg,
tempTBLv,
tblDescription,
UserDeskRep
where
((
   UserName      == UserDeskRep.OwnName and
   OborCFG.Nrep + cgReport_30000  == UserDeskRep.nRep
))
;

procedure CreateVar;
{
  var Marker, num, gliPickedCount, k : longint;
  var MarkerName: string;
  var nField : integer;
  var cKau : comp;

//-----
  pXL.CreateVar(sXLSFileName);
//-----
  pXL.SetStringVar('Title', 'Xls-Отчет');
  pXL.SetStringVar('MainName', Oborcfg.REPNAME);
//-----
  pXL.SetStringVar('CommonFormHeader', CommonFormHeader);
  if OborCfg.WFILTER[8] <> 0
  {
    if GetFirst UserDeskRep = tsOk
    {
      pXL.SetStringVar('FPDate_Name',OborCfg.sRep[8]);
      pXL.SetDateVar('FPDate_Beg', UserDeskRep.ResDate[1]);
      pXL.SetDateVar('FPDate_End', UserDeskRep.ResDate[2]);
      pXL.SetDateVar('Date1', UserDeskRep.ResDate[3]);
     }
  }
  iSQLFilts.ReadFiltValue(OborCFG.Nrec);
  for (nField := 1; nField<=7;nField++)
  {
    if OborCFG.WFILTER[nField] <> 0
    {
      iSQLFilts.ReadOneFiltValueToMarker(OborCFG.Nrec,nField);
      MarkerName := iSQLFilts.GetFiltMarkerName(OborCFG.Nrec,nField);
      Marker := InitMarker(MarkerName, 8, 100, 5);
      gliPickedCount := GetMarkerCount(Marker);

      if (gliPickedCount > 0)
      {
        GetMarker(Marker, 0, cKau);
        pXL.SetStringVar('filt'+String(nField)+'_Name',OborCFG.sRep[nField]);
        if (gliPickedCount = 1)
          pXL.SetStringVar('filt'+String(nField)+'_Value', if (GetAnyKAU(cgAkNoDef, OborCFG.WFILTER[nField], cKau), GivenAnName(1), ''));
        else
          pXL.SetStringVar('filt'+String(nField)+'_Value', 'Множественный выбор' + ' (' + string(gliPickedCount) + ')');
      }
      else
      {
        pXL.SetStringVar('filt'+String(nField)+'_Name',OborCFG.sRep[nField]);
        pXL.SetStringVar('filt'+String(nField)+'_Value','Не установлен');
      }
      DoneMarker(Marker, MarkerName);
    }
    else
    {
      pXL.SetStringVar('filt'+String(nField)+'_Name','');
      pXL.SetStringVar('filt'+String(nField)+'_Value','');
    }
  }

//-----
  pXL.PublishVar;
}

procedure InitExcel;
{
  if (Trim(OborCFG.column) = '')
  {
    ID := +OborCfg.nRep+'_'+Replace(OborCfg.REPNAME,' ','_');
    ID := Replace(ID,'(','_');
    ID := Replace(ID,')','_');
     Set sXLTFileName := TranslatePath('%ClientStartPath%') + 'XLS\DSQlBuildReps\' + ID + '.xlt';
    //if (not ReadMyDsk(sXLTFileName,ID,true))  {}
  }
  else
    sXLTFileName := TranslatePath(OborCFG.column);

}

procedure PrintField(num : word);
{
   case tblDescription.fldType[num] of
   ftComp                             : pXL.SetTblStringFldValue(tblDescription.fldName[num], String(tempTBLv.cComp[tblDescription.fldOrd[num]],0,0));
   ftChar,ftString,ftZString          : pXL.SetTblStringFldValue(tblDescription.fldName[num], tempTBLv.sString[tblDescription.fldOrd[num]]);
   ftSingle,ftDouble                  : pXL.SetTblNumberFldValue(tblDescription.fldName[num], tempTBLv.dDouble[tblDescription.fldOrd[num]]);
   ftByte,ftWord,ftInteger, ftLongInt : pXL.SetTblNumberFldValue(tblDescription.fldName[num], tempTBLv.wWord[tblDescription.fldOrd[num]]);
   ftDate                             : pXL.SetTblDateFldValue(tblDescription.fldName[num],   tempTBLv.dDate[tblDescription.fldOrd[num]]);
   ftTime                             : pXL.SetTblStringFldValue(tblDescription.fldName[num], TimeToStr(tempTBLv.tTime[tblDescription.fldOrd[num]],'HH:MM:SS'));
   end;
}
//------------------------------------------------------------------------------------------------------------
function PrintToExcel(_OborCFG : comp) : boolean;
{
  var k1, ColumnCol : word;
  PrintToExcel := true;
  pXL.CreateTbl('tempTBLv');
  ColumnCol := wSQLBuildReport.ColumnsCol;
  for (k1:=1; k1<=ColumnCol; k1++)
  {
    if tblDescription.fldName[k1] <> '' or  tblDescription.fldType[k1] <> 0
    {
      pXL.CreateTblFld(tblDescription.fldName[k1]);
    }
  }

//-----------------------------------------------------------------
  _loop tempTBLv
  {
    if (NOT NextVisual)
    {
      PrintToExcel :=false;
      PutCommand(cmError);
      pXL.DisConnectExcel;
      if (VisualizationActive())
        stopvisual('',0);
      Exit;
    }
    pXL.ClearTblBuffer;

    for (k1:=1; k1<=ColumnCol; k1++)
    {
      if tblDescription.fldName[k1] <> '' or  tblDescription.fldType[k1] <> 0
      {
        PrintField(k1);
      }
    }
    pXL.InsTblRow;
  }

  pXL.PublishTbl('tempTBLv');
  SetVisualHeader('Открытие Excel');
  pXL.LoadReport(sXLSFileName);
  pxl.GetLastError;
  pXL.DisConnectExcel;
  if (VisualizationActive())
    stopvisual('',0);
}
//------------------------------------------------------------------------------------------------------------
procedure CreateShablon(_OborCFG : comp);
{
  if GetFirst OborCFG where ((_OborCFG == OborCFG.Nrec)) = tsOk
  {
    ReReadREcord();
    if GetFirst tblDescription = tsOk
    {
      StartNewVisual(vtNumericVisual,vfTimer+vfBreak+vfConfirm , 'Печать отчета...', 1);
      InitExcel;
      Set sXLSFileName := pXL.CreateXLT(sXLTFileName, false);
      CreateVar;
      pXL.CreateTbls(sXLSFileName);
      PrintToExcel(OborCFG.Nrec);
      if ExistFile(sXLSFileName)
      {
        OborCFG.column := sXLSFileName;
        update current OborCFG;
      }
    }
  }
}

procedure PrintToShablon(_OborCFG : comp);
{
  if GetFirst OborCFG where ((_OborCFG == OborCFG.Nrec)) = tsOk
  {
    ReReadREcord();
    if GetFirst tblDescription = tsOk
    {
      StartNewVisual(vtNumericVisual,vfTimer+vfBreak+vfConfirm, 'Печать отчета...', 1);
      InitExcel;
      Set sXLSFileName := pXL.CreateReport(sXLTFileName, True);
      CreateVar;
      pXL.CreateTbls(sXLSFileName);
      PrintToExcel(OborCFG.Nrec);
    }
  }
}
end.
