/*************************************************************************************************\
* Наименование:  отчеты на DSQL                                                                   *
* Контур/Модуль: Утилиты                                                                          *
* Примечание:                                                                                     *
* Вид работы  |Номер         |Дата    |Исполнитель              |Проект                           *
* ----------------------------------------------------------------------------------------------- *
\*************************************************************************************************/

#include SQLFilts.vih
#include ViewResult.vih
#include SQLPrintToXLT.vih
//include SQLRunReport.vih
#include SQLBuildReport.vih

Interface SQLRunReport 'Отчет', EscClose,cyan;
  Show at (,,90,20);

File MacroFile;

Create view
var
 _OborCFG : comp;
 sDescr, sDesgr : string;
 wSQLFilts : SQLFilts;
 wViewResult : ViewResult;
 wSQLBuildReport : SQLBuildReport;
 ColumnCol : LongInt;

 SQLPrint : SQLPrintToXLT;

As Select
 *
from
  OborCfg,
  tblDescription,
  tempTBLv,
  UserDeskRep,

  AttrNam MacrosAttrNam,
  AttrVal MacrosAttrVal
 where
((
    _OborCFG == OborCfg.Nrec
    and    UserName      == UserDeskRep.OwnName
   and OborCFG.Nrep + cgReport_30000  == UserDeskRep.nRep

   and 'Макрос'     == MacrosAttrNam.Name
   and word(9400)   == MacrosAttrNam.WTABLE
   and word(9400)   == MacrosAttrVal.WTABLE
   and OborCfg.Nrec == MacrosAttrVal.cRec
   and MacrosAttrNam.Nrec == MacrosAttrVal.CATTRNAM
))
;
parameters
 _OborCFG
;

tabbedSheet top RunSql
  Show at (,,,17);
embedded embSQLFilts 'Фильтры' interface wSQLFilts end;
embedded embViewResult 'Результат' interface wViewResult end;
end;
Screen scSets 'Основная'('',,sci14Esc);
Show at (,18,,);
buttons
  cmValue12,,, 'Выполнить',,;
  cmValue13,Default,, 'Печать',,;
  cmClose,,, 'Выход',,;
<<
             <. Сформировать .>       <.    Печать    .>       <.    Отмена    .>

>>

end;

function RunMacros : boolean;
{
 if OborCFG.iFILTER[3] = 0
   Exit;
 var s,SQLmacros : string;
 SQLmacros:= GetPathParameter('Files','TmpFilesDirectory',0)+'SQLmacros.bas';
 DeleteFile(SQLmacros);
 if GetFirst MacrosAttrNam = tsOk
   if GetFirst MacrosAttrVal = tsOk
   {
     MacroFile.OpenMemo(MacrosAttrVal.vLvar);
     if MacroFile.GetSize > 0
     {
       MacroFile.seek(0);
       while not MacroFile.EOF do
       {
         MacroFile.ReadLn(s);
         LogStrToFile(SQLmacros, Oem2Ansi(s));
       }
     }
   }

 if not xlImportModule(SQLmacros) message ('Ошибка загрузки макроса');
   if not xlRunMacro(OborCfg.sRep[9]) message('Ошибка запуска макроса');
 //    DeleteFile(SQLFmacros);

result := true;
}

procedure PrintField(num, row1, col1 : longint);
{
   case tblDescription.fldType[num] of
     ftComp                              : XlStWriteToMatrix(row1, col1, ''''+String(tempTBLv.cComp[tblDescription.fldOrd[num]],0,0));
     ftChar,ftString,ftZString           : XlStWriteToMatrix(row1, col1, ''''+tempTBLv.sString[tblDescription.fldOrd[num]]);
     ftSingle,ftDouble                   : XlDoWriteToMatrix(row1, col1, tempTBLv.dDouble[tblDescription.fldOrd[num]]);
     ftByte,ftWord,ftInteger, ftLongInt  : XlDoWriteToMatrix(row1, col1, tempTBLv.wWord[tblDescription.fldOrd[num]]);
     ftDate                              : XlDtWriteToMatrix(row1, col1, tempTBLv.dDate[tblDescription.fldOrd[num]]);
     ftTime                              : XlStWriteToMatrix(row1, col1, TimeToStr(tempTBLv.tTime[tblDescription.fldOrd[num]],'HH:MM:SS'));
   end;
}

procedure PrintExcel;
{
  ColumnCol := wSQLBuildReport.ColumnsCol;

  wViewResult.ReDrawCaption;
  wViewResult.ReDrawTable;

  var col1, r1, k1 : longint;
  var NewExcel : boolean;
  StartNewVisual(vtRotateVisual, vftimer, 'Печать отчета...', 0);
  xlKillExcel;
  if OborCFG.column<>''
  {
    XLCreateNewExcelWithTemplate(OborCFG.column,true);
    NewExcel := false;
    if (not xlIsExcelValid)
    {
     if (VisualizationActive())
      stopvisual('',0);
      message('Ошибки при вызове Exel!файл шаблона не обработан'+Chr(13)+'Обратитесь к системному администратору',OkButton+error);
      exit;
      abort;
    }
  }
  else
  {
    // открыть файл
    xlOpenNewExcel(true);
    NewExcel := true;
  }
  if (xlIsExcelValid)
  {

    xlCreateMatrix( RecordsInTable(#tempTBLv), ColumnCol);
    if GetFirst tblDescription = tsok
    {
      r1:=0;

      _loop tempTBLv
      {
        col1:=0;
        r1++;
        for (k1:=1;k1<=ColumnCol;k1++)
        {
          col1++;
          if tblDescription.fldName[k1] <> '' or  tblDescription.fldType[k1] <> 0
          {
            PrintField(k1,r1,col1);
          }
        }
      }
    }

  }
  xlWriteMatrixToExcel(OborCFG.IFilter[0],1);
  xlFreeMatrix();
  RunMacros;
  xlKillExcel;

  if (VisualizationActive())
    stopvisual('',0);
}

procedure SetVisRes;
{
  if OborCFG.IFILTER[2] = 1
  {
    SetTabbedSheetVisible (embViewResult,true);
    ShowButton (scSets,  cmValue12, true);
    wViewResult.ReDrawCaption;
    wViewResult.ReDrawTable;
  }
  else
  {
    SetTabbedSheetVisible (embViewResult,false);
    ShowButton (scSets,  cmValue12, false);
  }
}

function CheckDates : boolean;
{
  CheckDates := true;
  if (OborCfg.WFILTER[8] and 1)<> 0
  {
    if UserDeskRep.RESDATE[2] < UserDeskRep.RESDATE[1]
    {
      message('Дата начала не может быть больше даты конца', Error);
      CheckDates := false;
    }
     if word(UserDeskRep.RESDATE[2])=0 or word(UserDeskRep.RESDATE[1])=0
     {
       message('Не заполнены даты!', Error);
       CheckDates := false;
     }

  }
}

HandleEvent
cmInit:
{
  if boGetTune('USERTUNE.AccessToCreateSQlRep')=false
    SetHelpAndStatusContext (embSQLFilts,-1, sci1Esc, '');
  delete all from tblDescription;
  delete all from tempTBLv;
  SetWindowTitle(wnMainWindow, OborCFG.nRep +' '+OborCFG.RepName);
  SetVisRes;
  wSQLFilts.ReInit(OborCFG.Nrec);
  cfsSetProp('F_USERREPORT::SQLRUNREPORT', cfpRectA_X, 0);
  cfsSetProp('F_USERREPORT::SQLRUNREPORT', cfpRectA_Y, 0);
  cfsSetProp('F_USERREPORT::SQLRUNREPORT', cfpRectB_X, 90);
  cfsSetProp('F_USERREPORT::SQLRUNREPORT', cfpRectB_Y, 20);
}
cmValue12:
{
  if GetFirst OborCFG = tsok
    if GetFirst UserDeskRep = tsOk
    { }

  if not CheckDates
    exit;

  if wSQLBuildReport.SQL_Run(OborCFG.Nrec)
    if GetFirst tblDescription = tsok
      if OborCFG.IFILTER[2] = 1
      {
        wViewResult.ReDrawCaption;
        wViewResult.ReDrawTable;
        SetFormat(embViewResult);
      }
}

cmValue13:
{
  var bCanPrint : boolean;
  bCanPrint := false;
  if GetFirst OborCFG = tsok
    if GetFirst UserDeskRep = tsOk
    { }
  if not CheckDates
    exit;

  if wSQLBuildReport.SQL_Run(OborCFG.Nrec)
    if GetFirst tblDescription = tsok
      bCanPrint := true;

  if bCanPrint
    if OborCfg.iFilter[1] = 0
      PrintExcel
    else
      SQLPrint.PrintToShablon(OborCFG.Nrec);
}

cmEdit:
{
  if boGetTune('USERTUNE.AccessToCreateSQlRep')=true
  {
    wSQLBuildReport.SQLEdit(OborCFG.Nrec);
    SetVisRes;
  }
}

End;
end.
