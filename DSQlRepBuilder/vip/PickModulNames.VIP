/*************************************************************************************************\
* Наименование:  отчеты на DSQL                                                                   *
* Контур/Модуль: Утилиты                                                                          *
* Примечание:                                                                                     *
* Вид работы  |Номер         |Дата    |Исполнитель              |Проект                           *
* ----------------------------------------------------------------------------------------------- *
\*************************************************************************************************/

//----------------------------------------------------------------------
Interface PickModulNames 'Выбор модулей'
                  DoAccept, EscClose, Cyan;
//----------------------------------------------------------------------
  show at ( , 6, 80 , 25);
Create View
var
  ModulCode : string;
  lMarker: longint;
  MarkerName : string;
From
 ContourComponents

Condition bySelected = foundmarker(lMarker, ContourComponents.Atl_Nrec)
;

Parameters
  ModulCode,
  MarkerName;

Browse  br1 (,,sci1InsPM);
Table ContourComponents;
//show at ( , , , 7);
Fields
  { Font = {Color =
         if(FoundMarker(lMarker, ContourComponents.Atl_Nrec), 19, 0)}};
  if(foundmarker(lMarker, ContourComponents.Atl_Nrec),'v', ''):[1], skip;
  ContourComponents.Name 'Идентификатор модуля' : [21], Protect;
  ContourComponents.TITLE 'Название модуля'  : [54], Protect;
end;

HandleEvent
cmDefault:
{
  ModulCode:= '';
  if GetMarkerCount(lMarker) > 0
  _loop ContourComponents where foundmarker(lMarker, ContourComponents.Atl_Nrec)
  {
    if ModulCode = ''
      ModulCode := ContourComponents.Name;
    else
      ModulCode := ModulCode +' '+ContourComponents.Name;
  }
  if GetMarkerCount(lMarker) = 0
    InsertMarker(lMarker, ContourComponents.Atl_Nrec);

}

cmMarkUnMark:
{
  if (FoundMarker(lMarker, ContourComponents.Atl_Nrec))
  {
    DeleteMarker(lMarker, ContourComponents.Atl_Nrec);
  }
  else
  {
    InsertMarker(lMarker, ContourComponents.Atl_Nrec);
  }
  if (getnext ContourComponents)=tsOk {};

  SetWindowTitle(wnMainWindow, 'Выбор модулей. Выбрано:'+ GetMarkerCount(lMarker));
   ReScanPanel(#ContourComponents);
}
cmSelectAll:
{
  ClearMarker(lMarker);
  StartNewVisual(vtRotateVisual, vfTimer+vfBreak, ''#3'Выбор ...'#13#3, 1);
  _try
  {
    pushpos(#ContourComponents);
    _loop ContourComponents
    {
      InsertMarker(lMarker, ContourComponents.Atl_Nrec);
      NextVisual;
    }
  } // try
  _except
   on ExUserBreak :
   {
     ClearMarker(lMarker);
   }
   _finally
   {
     poppos(#ContourComponents);

     ReReadRecord(#ContourComponents);
     StopVisual('',0);
   }
   SetWindowTitle(wnMainWindow, 'Выбор модулей. Выбрано:'+ GetMarkerCount(lMarker));
    ReScanPanel(#ContourComponents);
}

cmUnSelectAll:
{
  ClearMarker(lMarker);
  SetWindowTitle(wnMainWindow, 'Выбор модулей. Выбрано:'+ GetMarkerCount(lMarker));
  ReScanPanel(#ContourComponents);
}

cmInit:
{
 var CurRecord : comp;
 lmarker := initmarker(MarkerName, 8, 100, 10);
 GetMarker(lmarker, 0, CurRecord);

 if GetFirst ContourComponents where ((CurRecord == ContourComponents.Atl_Nrec)) = tsOk {}
 SetWindowTitle(wnMainWindow, 'Выбор модулей. Выбрано:'+ GetMarkerCount(lMarker));
}
cmDone:
{
  donemarker(lmarker, MarkerName);
}
cmHotKeys :
  PutHotCommand(RunMenu('mnuSetModulFilt'));
cmValue36:
{
  if ConditionActive(tcbySelected)
    PopCondition(tcbySelected)
  else
    PushCondition(tcbySelected);
ReReadRecord;
}

End;
End.

mnuSetModulFilt Menu
{
-'Установить\снять фильтр по выбранным', cmValue36, '', , , , sci1Esc;
}
