Create view MyFindPodr
From
  KatPodr
;

//******************************************************************************

Function MyFindSaldoMC(c: comp; d: date): boolean;
{
  MyFindSaldoMC := (GetFirst SaldoMC
                        where ((
                                 0 == SaldoMC.SP     AND
                                 0 == SaldoMC.cMC    AND
                                 0 == SaldoMC.cMOL   AND
                                 0 == SaldoMC.cParty AND
                                 c == SaldoMC.cPodr  AND
                                 d << SaldoMC.dSaldo
                              )) = tsOk);

  if (MyFindSaldoMC)
    if MyFindPodr.GetFirst KatPodr where ((c == KatPodr.NRec)) = tsOk
      Message('Операции для склада "' + MyFindPodr.KatPodr.Name + '" разрешены с ' + string(SaldoMC.dSaldo));
}

//******************************************************************************

Function IsOstOnMSPrPer(In_cMc: comp; in_Date: Date): boolean;
{
  IsOstOnMSPrPer := FALSE;

  if SkPr <> 0
    Exit; //для произв. не надо

  if wGetTune('MCUsl.ModOrder') = 0
    Exit; //разрешать

  if wGetTune('Oper.ClosePodr') = 0 //по предприятию
    {
      case wGetTune('MCUsl.ModOrder') of
      //==============================================================================
        1: IsOstOnMSPrPer := ( SkPr = 0 ) AND ( in_Date < dGetTune('TekMonth') )
      //==============================================================================
        2: IsOstOnMSPrPer := ( SkPr = 0 ) AND ( in_Date <= dGetTune('Oper.DateClosePeriod') );
      //==============================================================================
      end;

      Exit;
    }

  //******************************************************************************
  //по складам

  var cPodr_  : comp; cPodr_  := 0;
  var cPodr2_ : comp; cPodr2_ := 0;

  case KatSopr.vidSopr of
  //==============================================================================
    101  //(Товарная накладная на прием товара)
  , 108: //(Товарная накладная на прием молочного сырья)
      {
        cPodr_ := 0;

        While GetFirst SoprOrdB  where ((KatSopr.NRec == SoprOrdB.cSoprDoc AND
                                               cPodr_ << SoprOrdB.cPodr)) = tsOk
          {
            cPodr_ := SoprOrdB.cPodr;
            if GetFirst SaldoMC
                where ((
                                      0 == SaldoMC.SP    AND
                                      0 == SaldoMC.cMC   AND
                         SoprOrdB.cPodr == SaldoMC.cPodr AND
                                      0 == SaldoMC.cMOL  AND
                                      0 == SaldoMC.cParty AND
                                in_date << SaldoMC.dSaldo
                      )) = tsOk
              {
                cPodr_ := SoprOrdB.cPodr;
                IsOstOnMSPrPer := TRUE;
                Break;
              }
          }
      }

  //==============================================================================
    102 //(Товарная накладная на прием возвращ. товара от консигнаторов)
  , 103 //(Товарная накладная на прием товара на консигнацию)
  , 106 //(Накладная на возврат товаров по рекламации в продаже)
  , 151 //(акт реализации принятых на консигнацию МЦ)
  , 502 //(накладная на приход готовой продукции из производства /подразделение-склад/ )
  , 503 //(накладная на возврат сырья из производства /подразделение-склад/ )
  , 552 //(накладная на возврат из ремонта не использованных ТМЦ /подразделение-склад/ ))
  , 611 //(акт инвентаризации о излишке)
      : cPodr_ := KatSopr.cPodrTo;

  //==============================================================================
    201 //(Товарная накладная на отпуск товара)
  , 202 //(Товарная накладная на отпуск товара на консигнацию)
  , 203 //(Товарная накладная на возврат товара консигнанту)
  , 204 //(Акт на списание)
  , 206 //(Накладная на возврат товаров по рекламации при закупке)
  , 210 //(акт передачи оборудования в монтаж)
  , 229 //(акт передачи материалов на строительство)
  , 251 //(акт реализации переданных на консигнацию МЦ)
  , 501 //(накладная на отпуск в производство /склад-подразделение/ )
  , 551 //(накладная на отпуск для ремонта  /склад-подразделение/ )
  , 553 //(Акты выполненных ремонтов подрядным способом  //RM_
  , 554 //(Акты выполненных ремонтов собственными силами  //RM_
  , 557 //(Акты выполненных ремонтов на заказ  //RM_
  , 521 //(накладная на отпуск  модуля "Давальческая обработка")
  , 601 //(накладная на передачу товаров в ОС)
  , 602 //(накладная на передату товаров в МБП)
  , 603 //(накладная на передачу товаров в розницу)
  , 604 //(накладная на передачу МБП в Матценности)
  , 605 //(накладная на передачу товаров в НМА)
  , 612 //(акт инвентаризации о недостатке)
  , 621 //(акт комплектации)
  , 622 //(акт разукомплектации)
  , 2201//(Для сторонних организаций - Товарная накладная на отпуск товара)
      : cPodr_ := KatSopr.cPodrFrom;

  //==============================================================================
    //504 (Акт на списание из производства /подразделение-.../ )
    //505 (межцеховая накладная (полуфабрикат)/подразделение-подразделение/ )
    //506 (накладная на перемещение производства (МЦ)/подразделение-подразделение/ )
    ///522 (накладная на возврат модуля "Давальческая обработка")
    //523 (накладная на возврат сырья модуля "Давальческая обработка")
    ///532 (накладная на расход сырья/по накладной 502 приход ГП)
    //535 (накладная на расход сырья/по накладной 505 межцеховая)

  //==============================================================================
    600 //(накладная на внутреннее перемещение /склад-склад/)
  , 606 //(транзитная накладная /склад-склад/)
  , 607 //(накладная на внутреннее перемещение в ТОРО)
  , 630 //(акт пересортицы)
  , 632 //(акт на перемещение)
      :
        {
          cPodr_ := KatSopr.cPodrFrom;
          cPodr2_ := KatSopr.cPodrTo;
        }

  //==============================================================================
    //900-920 (Розничная торговля)

  //==============================================================================
    else
        cPodr_ := -1;
  //==============================================================================
  end; //c


  if (NOT IsOstOnMSPrPer)
    IsOstOnMSPrPer := MyFindSaldoMC(cPodr_, in_date);

  if (NOT IsOstOnMSPrPer)
    if (cPodr2_ <> 0)
      IsOstOnMSPrPer := MyFindSaldoMC(cPodr2_, in_date)

  In_cMc := comp(0);
}
